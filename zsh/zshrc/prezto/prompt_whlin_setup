#
# A colorful, friendly, multiline theme with some handy features.
#
# Authors:
#   Paul Gideon Dann <pdgiddie@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Features:
#   - Simple VCS branch, staged, and unstaged indication.
#   - Prompt character is different in a VCS repository.
#   - Last command exit status is displayed when non-zero.
#
# Screenshots:
#   http://i.imgur.com/rCo3S.png
#

function +vi-set_novcs_prompt_symbol {
  _prompt_whlin_symbol='$'
}

function +vi-set_vcs_prompt_symbol {
  _prompt_whlin_symbol='$'
}

function +vi-git-untracked {
  # Check for untracked files, since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    hook_com[unstaged]+='%F{white}● %f'
  fi
}

### Compare local changes to remote changes
function +vi-git-st {
local ahead behind remote
    local -a gitstatus

    # Are we on a remote-tracking branch?
    remote=${$(git rev-parse --verify ${hook_com[branch]}@{upstream} \
        --symbolic-full-name 2>/dev/null)/refs\/remotes\/}

    if [[ -n ${remote} ]] ; then
        # for git prior to 1.7
        # ahead=$(git rev-list origin/${hook_com[branch]}..HEAD | wc -l)
        ahead=$(git rev-list ${hook_com[branch]}@{upstream}..HEAD 2>/dev/null | wc -l)
        (( $ahead )) && gitstatus+=( "%B%F{yellow}⬆ %f%b" )

        # for git prior to 1.7
        # behind=$(git rev-list HEAD..origin/${hook_com[branch]} | wc -l)
        behind=$(git rev-list HEAD..${hook_com[branch]}@{upstream} 2>/dev/null | wc -l)
        (( $behind )) && gitstatus+=( "%B%F{yellow}⬇ %f%b" )

        #hook_com[unstaged]+="${gitstatus}"
        hook_com[branch]+="${gitstatus}"
    fi
}


function prompt_whlin_precmd {
  _prompt_whlin_pwd="${PWD/#$HOME/~}"
  vcs_info
}

function prompt_whlin_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz vcs_info
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_whlin_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_mseri_colors=(
      "%F{81}"  # Turquoise
      "%F{75}"  # Pastel Cyan
      "%F{215}" # Light Orange
      "%F{166}" # Orange - not used
      "%F{119}" # Pastel Limegreen
    )
  else
    _prompt_mseri_colors=(
      "%F{cyan}"
      "%F{magenta}"
      "%F{yellow}"
      "%F{red}"
      "%F{green}"
    )
  fi

  ## Set git-info parameters.-----
  #zstyle ':prezto:module:git:info' verbose 'yes'
  #zstyle ':prezto:module:git:info:ahead' format ' %%B%F{yellow}⬆ %f%%b'
  #zstyle ':prezto:module:git:info:behind' format ' %%B%F{yellow}⬇ %f%%b'
  #zstyle ':prezto:module:git:info:branch' format '%F{green}%b%f'
  #zstyle ':prezto:module:git:info:stashed' format ' %%B%F{cyan}✭ %f%%b'

  #zstyle ':prezto:module:git:info:clean' format ' %F{green}✔ %f'
  #zstyle ':prezto:module:git:info:dirty' format ' %F{red}✗%f'
  #zstyle ':prezto:module:git:info:untracked' format ' %%B%F{white}◼ %f%%b'

  #zstyle ':prezto:module:git:info:keys' format \
      #'prompt' ' %F{green}(%f$(coalesce "%b" "%p" "%c")${git_info[rprompt]}%s%F{green})%f' \
      #'rprompt' '%A%B%S%C%D%U%u'
  ## ---- end ----------------------

  # ---- vcs_info -----------------
  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%F{green}...%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%F{yellow}--- COMMAND ---%f'

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' check-for-changes true
  zstyle ':vcs_info:*' formats ' on %F{magenta}%b%f| %c%u'
  zstyle ':vcs_info:*' actionformats ' on %F{magenta}%b%f%c%u %F{yellow}(%a)%f'

  zstyle ':vcs_info:*' stagedstr '%F{green}✚ %f'
  zstyle ':vcs_info:*' unstagedstr '%F{red}✖ %f'

  # Set vcs_info hooks.
  # NOTE: Prior to Zsh v4.3.12, there are no static hooks, no vcs_info_hookadd
  #       function, and no 'no-vcs' hook.
  zstyle ':vcs_info:*+start-up:*' hooks set_novcs_prompt_symbol
  zstyle ':vcs_info:git*+set-message:*' hooks set_vcs_prompt_symbol \
                                                git-untracked \
                                                git-st
  zstyle ':vcs_info:*+set-message:*' hooks set_vcs_prompt_symbol
  # ---- end ----------------------

  # Define prompts.
  PROMPT="
[${_prompt_mseri_colors[3]}%n%f@${_prompt_mseri_colors[2]}%m%f] ${_prompt_mseri_colors[5]}%~%f"'${vcs_info_msg_0_}'"
» "
  RPROMPT=''

}

prompt_whlin_setup "$@"
