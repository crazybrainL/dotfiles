#
# A colorful, friendly, multiline theme with some handy features.
#
# Authors:
#   Paul Gideon Dann <pdgiddie@gmail.com>
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#
# Features:
#   - Simple VCS branch, staged, and unstaged indication.
#   - Prompt character is different in a VCS repository.
#   - Last command exit status is displayed when non-zero.
#
# Screenshots:
#   http://i.imgur.com/rCo3S.png
#
function +vi-set_novcs_prompt_symbol {
  _prompt_whlin_symbol='$'
}

function +vi-set_vcs_prompt_symbol {
  _prompt_whlin_symbol='$'
}

function prompt_whlin_precmd {
  _prompt_whlin_pwd="${PWD/#$HOME/~}"
  git-info
}

function prompt_whlin_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling git-info before each command.
  add-zsh-hook precmd prompt_whlin_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_mseri_colors=(
      "%F{81}"  # Turquoise
      "%F{75}"  # Pastel Cyan
      "%F{215}" # Light Orange
      "%F{166}" # Orange - not used
      "%F{119}" # Pastel Limegreen
      "%F{135}" # Purple
      "%F{161}" # Hotpink
      "%F{118}" # Limegreen
    )
  else
    _prompt_mseri_colors=(
      "%F{cyan}"
      "%F{magenta}"
      "%F{yellow}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:completing' format '%F{green}...%f'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '%F{yellow}--- COMMAND ---%f'

  # ----- git info -----
  # Set git-info parameters.
  zstyle ':prezto:module:git:info' verbose 'yes'
  zstyle ':prezto:module:git:info:branch' format "${_prompt_mseri_colors[1]}%b%f "
  zstyle ':prezto:module:git:info:commit' format "%F{white}%.7c%f"
  zstyle ':prezto:module:git:info:dirty' format "%%B%F{red}D%f%%b"
  zstyle ':prezto:module:git:info:modified' format "%F{red}!%f"
  zstyle ':prezto:module:git:info:clean' format "%F{green}●%f"
  zstyle ':prezto:module:git:info:ahead' format "%%B%F{yellow}⬆%f%%b"
  zstyle ':prezto:module:git:info:behind' format "%%B%F{yellow}⬇%f%%b"
  zstyle ':prezto:module:git:info:stashed' format "%%B${_prompt_mseri_colors[8]}✭%f%%b"
  zstyle ':prezto:module:git:info:untracked' format "%%B%F{white}?%f%%b"

  zstyle ':prezto:module:git:info:keys' format \
        'rprompt' '(%b%D%C%S%d%a%m%u)%A%B'
  # ----- end -----

  PROMPT='
[\
${_prompt_mseri_colors[2]}%n%f\
@\
${_prompt_mseri_colors[6]}%m%f]\
${_prompt_mseri_colors[5]}${_prompt_whlin_pwd}%f
» '
  RPROMPT='${git_info[rprompt]}'
}

prompt_whlin_setup "$@"
